plugins {
    id 'java'
    id 'groovy'
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "2.0.0"
    id 'maven-publish'
    id "com.gradleup.shadow" version "9.1.0"
    id 'pmd'
    id 'codenarc'
    id 'jacoco'
}

ext {
    pluginDescription = 'The plugin that allows you access to java keytool commands in gradle as task'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

configurations {
    plugin
    implementation.extendsFrom(plugin)

    [apiElements, runtimeElements].each {
        it.outgoing.artifacts.removeIf { it.buildDependencies.getDependencies(null).contains(jar) }
        it.outgoing.artifact(shadowJar)
    }

}

shadowJar {
    configurations = [project.configurations.plugin]
    archiveClassifier.set("")
    relocate 'xyz.ronella.trivial', "${project.group}.simple.keytool.trivial"
    relocate 'xyz.ronella.command.arrays', "${project.group}.simple.keytool.command.arrays"
    minimize()
}

pmd {
    consoleOutput = true
    toolVersion = "7.17.0"
    rulesMinimumPriority = 5
    ruleSetFiles = files('config/pmd/custom.xml')
    ruleSets = []
}

codenarc {
    toolVersion = "3.1.0"
}

gradlePlugin {
    plugins {
        website = 'https://github.com/rcw3bb/simple-keytool'
        vcsUrl = 'https://github.com/rcw3bb/simple-keytool'
        description = pluginDescription
        simpleKeytool {
            id = 'xyz.ronella.simple-keytool'
            displayName = 'Simple Keytool Plugin'
            description = pluginDescription
            implementationClass = 'xyz.ronella.gradle.plugin.simple.keytool.SimpleKeytoolPlugin'
            tags = ['ronella', 'simple-keytool', 'simple', 'keytool', 'java', 'cacert', 'automation',
                    'certificate management', 'management', 'security']
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    plugin 'xyz.ronella.casual:trivial-chunk:2.10.0'
    plugin 'xyz.ronella.casual:command-arrays:1.0.0'

    implementation gradleApi()
    implementation localGroovy()

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.8.2'

    testImplementation 'org.mockito:mockito-core:4.4.0'
    testImplementation 'org.mockito:mockito-inline:4.4.0'
}

pmdTest.enabled = false
codenarcTest.enabled = false

jar {
    enabled = false
    dependsOn shadowJar
}

test {
    dependsOn pmdMain, codenarcMain
    finalizedBy jacocoTestReport
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test
}